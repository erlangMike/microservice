
buildscript {
    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/"}
        maven { url "http://central.maven.org/maven2/"}
        maven {url "https://plugins.gradle.org/m2/"}
        maven {url "https://repo.spring.io/libs-milestone"}
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE"
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE'
    }
}


allprojects{
    //仓库配置
    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/"}
        maven { url "http://central.maven.org/maven2/"}
        maven {url "https://plugins.gradle.org/m2/"}
        maven {url "https://repo.spring.io/libs-milestone"}
    }
}

subprojects {

    //插件支持
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'groovy'
//    apply plugin: 'spring-cloud-contract'

    group 'com.bchain.platform'
    version '1.0-SNAPSHOT'

    // JVM 版本号要求
    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    // java编译的时候缺省状态下会因为中文字符而失败

    [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    //不加引用rt.jar报错
    compileJava {
        options.fork = true
        options.forkOptions.executable = "javac" // assumes that javac is on PATH
        options.compilerArgs << "-XDignore.symbol.file"
    }

    //定义版本号
    ext {
        springBootVersion = '1.5.10.RELEASE'
        springCloudVersion='Edgware SR3'
        springBootTestStarterVersion = '1.5.10.RELEASE'
        swagger2Version='2.8.0'
        springCloudZKVersion='1.2.0.RELEASE'
        springCloudEKVersion='1.2.0.RELEASE'

    }



    configurations {
        // 所有需要忽略的包定义在此
        //        all*.exclude group: 'commons-httpclient'
        compile.exclude module: 'spring-boot-starter-tomcat'
    }

    dependencyManagement {
        imports {
//            mavenBom 'org.springframework.cloud:spring-cloud-zookeeper-dependencies:1.2.1.RELEASE'
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Edgware.SR3'
        }
    }
    dependencies{
        // 依赖maven中不存在的jar
        ext.jarTree = fileTree(dir: 'libs', include: '**/*.jar')
        ext.rootProjectLibs = new File(rootProject.rootDir, 'libs').getAbsolutePath()
        ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')
        compile jarTree
    }
    // 显示当前项目下所有用于 compile 的 jar.
    task listJars(description: '输出当前项目全部jar包') << {
        configurations.compile.each { File file -> println file.name }
    }
    task cpWarsToDK(description: '把build后的包文件copy到docker中',type: Copy){
        from('/build/libs'){
            include '*.war'
        }
        into('/docker/libs/')
    }
}